###############################################################################
# Path configuration
###############################################################################
export PATH=$PATH:/usr/bin
export PATH=$PATH:/bin
export PATH=$PATH:/usr/sbin
export PATH=$PATH:/sbin
export PATH=$PATH:$HOME/go/bin
export PATH=$PATH:/opt/homebrew/bin

# Use asdf tools
export PATH=$PATH:$HOME/.asdf

# Use legacy HISTFILE for command search
export HISTFILE="${ZDOTDIR:-$HOME}/.zhistory"

###############################################################################
# Source Prezto
###############################################################################
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

###############################################################################
# Miscellany
###############################################################################

# Add zsh aliases
if [ -f ~/.zsh_aliases ]; then
  source ~/.zsh_aliases
fi

# Set colorful terminal
if [[ $TERM == xterm ]]; then
  TERM=xterm-256color;
fi

# User configuration
setopt NONOMATCH
setopt NOCORRECT

# Neovim as default editor
export EDITOR='nvim'

# Ripgrep configuration
export RIPGREP_CONFIG_PATH="$HOME/.ripgreprc"

# Brew autocomplete
if type brew &>/dev/null
then
  FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"

  autoload -Uz compinit
  compinit
fi

###############################################################################
# FZF
###############################################################################
if which fzf > /dev/null 2>&1; then
  source <(fzf --zsh)

  # FZF options
  export FZF_DEFAULT_COMMAND="fd --type file --hidden --strip-cwd-prefix"
  export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
  export FZF_CTRL_R_OPTS='--sort'
fi

###############################################################################
# Vi Mode
###############################################################################
bindkey -M viins 'jk' vi-cmd-mode
bindkey '^?' backward-delete-char

bindkey -M viins "$terminfo[cuu1]" history-substring-search-up
bindkey -M viins "$terminfo[cud1]" history-substring-search-down
bindkey -M viins "^u" clear-screen
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down
bindkey -M vicmd '^a' beginning-of-line
bindkey -M vicmd '^e' end-of-line
bindkey -M vicmd 'H' beginning-of-line
bindkey -M vicmd 'L' end-of-line
bindkey -M vicmd 'u' undo

# Make copy/paste with the system clipboard behave as expected with vim commands.
function cutbuffer() {
  zle .$WIDGET
  echo -n $CUTBUFFER | pbcopy
  region_highlight=("0 0 standout")
}

zle_cut_widgets=(
  vi-yank
  vi-yank-eol
)
for widget in $zle_cut_widgets
do
  zle -N $widget cutbuffer
done

function putbuffer() {
  zle copy-region-as-kill "$(pbpaste)"
  zle .$WIDGET
}

zle_put_widgets=(
  vi-put-after
  vi-put-before
)
for widget in $zle_put_widgets
do
  zle -N $widget putbuffer
done

# Edit command in vim
zle -N edit-command-line
bindkey -M vicmd E edit-command-line

###############################################################################
# asdf
###############################################################################
export ASDF_DIR="/opt/homebrew/opt/asdf/libexec"
if [[ -s "$ASDF_DIR/asdf.sh" ]]; then
  source "$ASDF_DIR/asdf.sh"
fi

###############################################################################
# zoxide
###############################################################################
eval "$(zoxide init zsh)"

###############################################################################
# Starship
###############################################################################

# Account for multiple source bug, https://github.com/starship/starship/issues/3418#issuecomment-2477375663
if [[ "${widgets[zle-keymap-select]#user:}" == "starship_zle-keymap-select" || \
      "${widgets[zle-keymap-select]#user:}" == "starship_zle-keymap-select-wrapped" ]]; then
    zle -N zle-keymap-select "";
fi

eval "$(starship init zsh)"
